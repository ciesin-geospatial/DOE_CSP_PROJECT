 ! COMRO
 Model 
   Constants
	Awj1 = 1.5/3.6e6			! convert to m/bar/s
	!Bsj1 = 1.2469e-08     	        !  m/s, OARO baseline salt permeability coefficient (Bartholomew & Mauter, 2018) 
	S  = 1200e-6   			! structural parameter, meter

	hf = 0.002 ! 0.00075      		! feed channel height   
	hp = 0.002 !1.62e-3  		    ! permeate channel height, taken from Jeon et al. A simple modeling approach for a forward osmosis system with a spiral wound module. Desalination. 2018 May 1;433:120â€“31. 

	i = 2					! vant Hoff factor (dissociation factor for NaCl)
	R = 8.314e-2 			! ideal gas constant
	T = 20 +273.15			! temperature, Kelvin
	MW = 58.44				! molecular weight of NaCl

	void = 0.75		! spacer porosity

    N = 3
	densityw = 995.1			! density of water, kg/m3, 997 kg/m3 is more accurate but staying consistent with the density-mass fraction fitted equation
	D = 1e-9				! diffusivity of NaCl, m2/s
	w=  1.34	!, > 0    	! width of membrane, m
	L=1
	
	nl=0.5

	eps= 1e-10
	
	Awk1= 4.2e-7
	Bsk1= 3.5e-8
	
	 xpoly[1]= -0.000420719536247e-3
	 xpoly[2]=  0.966292293659073e-3
	 xpoly[3]=  0.982129425760762e-3
	 
	 Patm= 1
	 Nstage= 1
   End Constants

   Parameters
	 
	 Qin= 468/24 !500000/24
	
	
	 Cin=  125		
	 !Rtq= 	0.4
	 Rt= 0.25 !+ eps
	  
	 Pmaxj1_= 65
	 Pmaxk1_= 85

	 
	 Ppk=1
	 
	
	 !Qr= (1-Rtq)*Qin , > 0 , < Qin

	 
	 eff_pump = 0.75
	 eff_erd  = 0.90
	
	 Perdoutk1 = Patm
	 Perdoutj1 = Patm
	 
	 irate = 0.078
	 years = 20 
	 
	 ecost = 0.07
	 
	 oaromemcost = 50
	 romemcost = 30
	 memrepfactor= 0.15    		! Annual membrane replacement factor
	 practical_inv_factor = 1.6 ! Practical investment factor to convert total equipment costs to total capital investment
	 maint_labor_factor= 0.02
	 chemical_factor = 0.01
	 
	 pumpcost = 53 * 3600 ! 53 $/bar/m3*h converted to $/bar/m3*s
	 
	 loadfactor= 0.9
   End Parameters

   Variables
	 !Rt=  0.5  , < 1 , > 0
	 !Rtq= 0.5 , < 1 , > 0	
	 Mfj[0] > 0 , < Qin*(750.6*(xpoly[1]*Cin^2 + xpoly[2]*Cin +xpoly[3])+ 995.1)/3600
	 !Qr > 0 , < Qin

	 Wj1= 1e2 , >= w ,  <= 1e5	
	 Wk1= 1e1 , >= w  ,  <= 1e2
	 
	 Lj1= 1e2 , >= L , <= 5e3
	 Lk1= 1e1 , >= L , <= 1e3
	 
	 
	 
	 Jwj[1:N] = 5.6e-7 , >= 0 		   , <= 1.1111e-04 ! m3/m2/s  --> rescale to LMH      >= 5.5556e-09 
	 Jsj[1:N]= 5.6e-7 ,  >= 0 		   , <= 1.3889e-04 ! kg/m2/s  --> rescale to g/m2/h
	 
	 Jwk[1:N]= 5.6e-7 ,  >= 2.2222e-08 , <= 1.6667e-05 ! m3/m2/s  --> rescale to LMH
	 Jsk[1:N]= 5.6e-7 ,  >= 0 		   , <= 1.3889e-05 ! kg/m2/s  --> rescale to g/m2/h 
	
 	 
	 Pfj[0:N]=Pmaxj1_ >= 0.5 *Pmaxj1_ , < Pmaxj1_ 
	 Pfjin=Pmaxj1_, > Pfj[N] , <= Pmaxj1_
	 Ppj[0:N] >= 1 
	 
	 Pfk[0:N]  >= 0.5 *Pmaxk1_ , <= Pmaxk1_ 

	 
	 Cfoutj= Cin/(1-Rtq), > Cin , <= 350
     CpNk1= 0.5 , > 0 , <= 0.5
	 
	 
	 !!!!! Adding SEC and LCOW sections
	 !Perdoutk1 >= 1 , < Pfk1_[N]
	 LCOW > 0 !, < 100
	 !SEC > 0 !, < 100
	 Bsj1 = .0133/3.6e6*(Awj1*3.6e6)^3 , >= .0133/3.6e6*(Awj1*3.6e6)^3 , <= .0133/3.6e6*(Awj1*3.6e6)^3 +eps !1e-7

   End Variables

   Intermediates
   
	!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!! COUNTER-CURRENT MODULE #1
	! Acfj1_=hf*w*void*int_nvj1_*nl*2
	! Acpj1_=hp*w*void*int_nvj1_*nl
	
	! Acfk1_=hf*w*void*int_nvk1_*nl*2
	
	
	 Amj1=Lj1*Wj1
	 Amk1=Lk1*Wk1
	
	 dhfj1= 2*(hf*Wj1)/(hf+Wj1)
	 dhpj1= 2*(hp*Wj1)/(hp+Wj1)
	
	 dhfk1= 2*(hf*Wk1)/(hf+Wk1)
	
	 Acfj=hf*Wj1*void
	 Acpj=hp*Wj1*void
	
	 Acfk=hf*Wk1*void
	 
	 
	 !Xrop=xpoly[1]*Crop^2 + xpoly[2]*Crop +xpoly[3]

	 Cpj[0]= Cin
	 Cfj[0]= Cfoutj
	 
	 Xfj[0]=xpoly[1]*Cfj[0]^2 + xpoly[2]*Cfj[0] +xpoly[3]
	 Xpj[0]=xpoly[1]*Cpj[0]^2 + xpoly[2]*Cpj[0] +xpoly[3]
	 
	 
	 
	 densityfj[0]=750.6*Xfj[0] + 995.1    
	 densitypj[0]=750.6*Xpj[0] + 995.1
	 
	 
	
	 Mpj[0]=	Qin*densitypj[0]/3600
	 
	 Mpj[1:N]=  Mpj[0:N-1] + Lj1*Wj1/N*(Jwj[1:N]*densityw + Jsj[1:N])
	 Xpj[1:N]= (Mpj[0:N-1]*Xpj[0:N-1] + Lj1*Wj1/N*Jsj[1:N])/Mpj[1:N]
	 
	 Mfj[1:N]= Mfj[0:N-1] + Lj1*Wj1/N*(Jwj[1:N]*densityw + Jsj[1:N])
     Xfj[1:N]= (Mfj[0:N-1]*Xfj[0:N-1] + Lj1*Wj1/N*Jsj[1:N])/Mfj[1:N]
	 
	
	 
	 Cpj[1:N]=(Cpj[0:N-1] + 750.6*Xpj[1:N]^2 + 995.1*Xpj[1:N])*0.5
	 Cfj[1:N]=(Cfj[0:N-1] + 750.6*Xfj[1:N]^2 + 995.1*Xfj[1:N])*0.5
	 
	 densityfj[1:N]=750.6*Xfj[1:N] + 995.1    
	 densitypj[1:N]=750.6*Xpj[1:N] + 995.1
	 
	 
	 viscfj[0:N]= 2.15e-3 *Xfj[0:N] + 9.80e-4
	 Refj[0:N]=Mfj[0:N]/Acfj*dhfj1/viscfj[0:N]
	 FFfj[0:N]=0.42 + 189.3/Refj[0:N]
	 Plfj[0:N]=FFfj[0:N]*(Mfj[0:N]^2)/(4*densityfj[0:N]*hf*Acfj^2)*10^(-5)
	 	 
	 
	 viscpj[0:N]= 2.15e-3 *Xpj[0:N] + 9.80e-4
	 Repj[0:N]=Mpj[0:N]/Acpj*dhpj1/viscpj[0:N]
	 FFpj[0:N]=0.42 + 189.3/Repj[0:N]
	 Plpj[0:N]=FFpj[0:N]*(Mpj[0:N]^2)/(4*densitypj[0:N]*hp*Acpj^2)*10^(-5)
	 	

	 Scfj[0:N]=viscfj[0:N]/densityfj[0:N]/D
	 Shfj[0:N]=0.46*(Refj[0:N]*Scfj[0:N])^0.36
	 kfj[0:N]=D/dhfj1*Shfj[0:N]
	 
	 ! densityf[1]=750.6*Xf[1] + 995.1
	 ! viscf[1]= 2.15e-3 *Xf[1] + 9.80e-4
	 ! Ref[1]= Mf[1]/Acf*dhf/viscf[1]
	 ! Scf[1]=viscf[1]/densityf[1]/D
	 ! Shf[1]=0.46*(Ref[1]*Scf[1])^0.36
	 ! kf1=D/dhf*Shf[1]
	 kfavgj[1:N]=(kfj[0:N-1]+ kfj[1:N])*0.5
	 
	 
	 Scpj[0:N]=viscpj[0:N]/densitypj[0:N]/D
	 Shpj[0:N]=0.46*(Repj[0:N]*Scpj[0:N])^0.36
	 kpj[0:N]=D/dhpj1*Shpj[0:N]
	 
	 ! densityp[1]=750.6*Xp[1] + 995.1
	 ! viscp[1]= 2.15e-3 *Xp[1] + 9.80e-4
	 ! Rep[1]= Mp[1]/Acp*dhp/viscp[1]
	 ! Scp[1]=viscp[1]/densityp[1]/D
	 ! Shp[1]=0.46*(Rep[1]*Scp[1])^0.36
	 ! kp1=D/dhp*Shp[1]
	 kpavgj[1:N]=(kpj[0:N-1] + kpj[1:N])*0.5
	 
	 Cfmj[1:N]=Cfj[1:N]*exp(Jwj[1:N]/kfavgj[1:N]) - Jsj[1:N]/Jwj[1:N]*(exp(Jwj[1:N]/kfavgj[1:N]) - 1)
	 Cpmj[1:N]=Cpj[1:N]*exp(-Jwj[1:N]*(S/D + 1/kpavgj[1:N]))+ Jsj[1:N]/Jwj[1:N]*(1 - exp(-Jwj[1:N]*(S/D + 1/kpavgj[1:N])))
	 
	 phiosmfmj[1:N]= 3.33e-6 *Cfmj[1:N]^2 +1.78e-4 *Cfmj[1:N] + 0.918
	 phiosmpmj[1:N]= 3.33e-6 *Cpmj[1:N]^2 +1.78e-4 *Cpmj[1:N] + 0.918
	 
	 Posmfj[1:N]= i*R*T/MW*phiosmfmj[1:N]*Cfmj[1:N]
	 Posmpj[1:N]= i*R*T/MW*phiosmpmj[1:N]*Cpmj[1:N]
	 
	 !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!! RO Module # 1
	 Mfk[0]=Mpj[N]
	 Xfk[0]=Xpj[N]
	 
	 Mpk[0]=0
	 Xpk[0]=0
	 
	 Mfk[1:N]= Mfk[0:N-1] - Lk1*Wk1/N*(Jwk[1:N]*densityw + Jsk[1:N])
	 Mpk[1:N]= Mpk[0:N-1] + Lk1*Wk1/N*(Jwk[1:N]*densityw + Jsk[1:N])
	 
	 Xfk[1:N]= (Mfk[0:N-1]*Xfk[0:N-1] - Lk1*Wk1/N*Jsk[1:N])/Mfk[1:N]
	 Xpk[1:N]= (Mpk[0:N-1]*Xpk[0:N-1] + Lk1*Wk1/N*Jsk[1:N])/Mpk[1:N]
	 
	 !Xpavgk[1:N]=(Xpk[0:N-1]*Mpk[0:N-1] + Xpk[1:N]*Mpk[1:N])/(Mpk[0:N-1]+Mpk[1:N])

	 !Xpsumk1[1:N]=Xpk[0:N-1] + Xpk[1:N]
	 
	 
	 densityfk[0:N]=750.6*Xfk[0:N] + 995.1    
	 viscfk[0:N]= 2.15e-3 *Xfk[0:N] + 9.80e-4
	 Refk[0:N]=Mfk[0:N]/Acfk*dhfk1/viscfk[0:N]
	 FFfk[0:N]=0.42 + 189.3/Refk[0:N]
	 Plfk[0:N]=FFfk[0:N]*(Mfk[0:N]^2)/(4*densityfk[0:N]*hf*Acfk^2)*10^(-5)
	 Scfk[0:N]=viscfk[0:N]/densityfk[0:N]/D
	 Shfk[0:N]=0.46*(Refk[0:N]*Scfk[0:N])^0.36
	 kfk[0:N]=D/dhfk1*Shfk[0:N]
	 kfavgk[1:N]=(kfk[0:N-1]+ kfk[1:N])*0.5
	 
	 Cpk[0]= 750.6*Xpk[0]^2 + 995.1*Xpk[0]
	 Cfk[0]= 750.6*Xfk[0]^2 + 995.1*Xfk[0]
	 
	 Cpk[1:N]=(Cpk[0:N-1] + 750.6*Xpk[1:N]^2 + 995.1*Xpk[1:N])*0.5
	 Cfk[1:N]=(Cfk[0:N-1] + 750.6*Xfk[1:N]^2 + 995.1*Xfk[1:N])*0.5
	 
	 Cfmk[1:N]=Cfk[1:N]*exp(Jwk[1:N]/kfavgk[1:N]) - Jsk[1:N]/Jwk[1:N]*(exp(Jwk[1:N]/kfavgk[1:N]) - 1)
	 phiosmfmk[1:N]= 3.33e-6 *Cfmk[1:N]^2 +1.78e-4 *Cfmk[1:N] + 0.918	 
	 phiosmpk[1:N]= 3.33e-6 *Cpk[1:N]^2 +1.78e-4 *Cpk[1:N] + 0.918	 

	 Posmfk[1:N]= i*R*T/MW*phiosmfmk[1:N]*Cfmk[1:N]
	 Posmpk[1:N]= i*R*T/MW*phiosmpk[1:N]*Cpk[1:N]	 


	 densitypk1=750.6*Xpk[N] + 995.1    
	 Qpk1= Mpk[N]/densitypk1
     !z[2:n] = z[1:n-1] + p[2:n]
	 !!! Experimental Additions
	 r1=(Mfj[N]-Mfj[0])/Mfj[N]
	 r2 = Mpk[N]/Mfk[0]
	 !Rtq = 1-Qr/Qin
	 Rtq= Qpk1*3600/(Qin) !+ Ma*3600/1191.56)
	 
	 
	
	 !Pfk[0]<=Pmaxk1_
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!			 
 ! ######  ########  ######  
! ##    ## ##       ##    ## 
! ##       ##       ##       
 ! ######  ######   ##       
      ! ## ##       ##       
! ##    ## ##       ##    ## 
 ! ######  ########  ###### 
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
	 Ppjout=Ppj[N] - Plpj[N]*Lj1/(2*N)	
	 Pfkout=Pfk[N] - Plfk[N]*Lk1/(2*N)



	 Pbk1in= eff_erd*(Pfkout-Perdoutk1)+ Ppjout
	 PQHpumpk1=Mpk[N]/densityfk[0] * (Pfk[0]-Ppjout)
	 PQBpumpk1=Mfk[N]/densityfk[0] * (Pfk[0]-Pbk1in)
	 SEC_Hpumpk1 = PQHpumpk1/eff_pump/Qpk1/36   !!! High-pressure pump for terminal RO, k1 stage
	 SEC_Bpumpk1 = PQBpumpk1/eff_pump/Qpk1/36
	 
	 Pbj1in= eff_erd*(Pfj[0] - Perdoutj1) + Perdoutk1
	 
	 PQHpumpj1=(Mfj[N]-Mfj[0])/densityfj[N] *(Pfjin- Perdoutk1)
	 PQBpumpj1= Mfj[0]/densityfj[N] * (Pfjin - Pbj1in)
	 PQLpumpj1= Mpj[0]/densitypj[0] * (Ppj[0] - Patm)
	 SEC_Hpumpj1 = PQHpumpj1/eff_pump/Qpk1/36   !!! High-pressure pump for CC module, j1 stage
	 SEC_Bpumpj1 = PQBpumpj1/eff_pump/Qpk1/36
	 
	 SEC_Lpumpj1 = PQLpumpj1/eff_pump/Qpk1/36

	 
	 SEC = SEC_Hpumpk1 + SEC_Bpumpk1 + SEC_Hpumpj1 + SEC_Bpumpj1 + SEC_Lpumpj1



!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
! ##        ######   #######  ##      ## 
! ##       ##    ## ##     ## ##  ##  ## 
! ##       ##       ##     ## ##  ##  ## 
! ##       ##       ##     ## ##  ##  ## 
! ##       ##       ##     ## ##  ##  ## 
! ##       ##    ## ##     ## ##  ##  ## 
! ########  ######   #######   ###  ### 
 !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
 
	 CR_factor = 1/((1 - (1 /(1 + irate) ^ years)) / irate)
	 
	 erdcost =  3134.7 *( (Mfj[0]/densityfj[0]*3600)^(0.58) + (Mfj[N]/densityfj[N]*3600)^(0.58))
	 pumptotalcost = pumpcost*(PQHpumpk1 + PQBpumpk1 + PQHpumpj1 + PQBpumpj1 + PQLpumpj1)
	 capex = (Amj1 * oaromemcost + Amk1 * romemcost + pumptotalcost + erdcost) * practical_inv_factor
	 memrepcost = (Amj1 * oaromemcost + Amk1 * romemcost)*memrepfactor
	 maintlaborcost = maint_labor_factor*capex
	 chemcost = chemical_factor*capex
	 energycost= SEC * ecost 
	 opex = memrepcost + chemcost + maintlaborcost
	 
   End Intermediates

   Equations
	Ppjout=1
	 !Mfj[0]= Qr*densityfj[0]/3600
	 
	! Mfj[0] <  Mpj[0]
	 
	 Rt= Mpk[N]*(1-Xpk[N])/(Mpj[0]*(1-Xpj[0]))

	
	
	

	 LCOW = (CR_factor*capex +  opex)/(Qpk1*3600*8760*loadfactor)  + energycost

	 ! Cpj[0]=750.6*Xpj[0]^2 + 995.1*Xpj[0]
	 ! Cfj[0]=750.6*Xfj[0]^2 + 995.1*Xfj[0]
	
	 
	 ! Ref[0:N]>100
	 ! Rep[0:N]>100
	 ! Ref[0:N]>2000
	 ! Rep[0:N]>2000
	 
	 ! Plf[0:N]<5
	 ! Plp[0:N]<5
	 !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
	 Pfj[1]=Pfj[0] + Plfj[0]*Lj1/(2*N)
	 Pfj[2:N]=Pfj[1:N-1] + Plfj[1:N-1]*Lj1/N 
	 Pfjin=Pfj[N] + Plfj[N]*Lj1/(2*N)
	 
	 Ppj[1]=Ppj[0] - Plpj[0]*Lj1/(2*N)
	 Ppj[2:N]=Ppj[1:N-1] - Plpj[1:N-1]*Lj1/N 
	 !Ppj[N]=Ppj[N-1] - Plpj[N-1]*Lj1/(2*N)	

	 
	 
	 Pfk[1]=Pfk[0] - Plfk[0]*Lk1/(2*N)
	 Pfk[2:N]=Pfk[1:N-1] - Plfk[1:N-1]*Lk1/N 
	 !Pfk[N]=Pfk[N-1] - Plfk[N-1]*Lk1/(2*N)
	 
	 
	 
	 ! Pfjin<=Pmaxj1_
	 ! Ppjout>=1
	 
	 Jwj[1:N]= Awj1*(Pfj[1:N] - Ppj[1:N] -(Posmfj[1:N]-Posmpj[1:N]))
	 Jsj[1:N]= Bsj1*(Cfmj[1:N] - Cpmj[1:N])
	 
	 Mfk[N]=Mfj[N]
	 Xfk[N]=Xfj[N]
	 

	 
	 Jwk[1:N]= Awk1*(Pfk[1:N] - Ppk -(Posmfk[1:N]-Posmpk[1:N]))
	 Jsk[1:N]= Bsk1*(Cfmk[1:N] - Cpk[1:N])


	 
	 !Cpavgk= 750.6*Xpavgk[N]^2 + 995.1*Xpavgk[N]
	 CpNk1=750.6*Xpk[N]^2 + 995.1*Xpk[N]
	
	
	
	
	!Cpj[1:N]<Cpj[0:N-1] ! prevent code from selecting recovery rate associated with increasing sweep (saline permeate) concentration when the bulk sweep concentration should be decreasing in the current convention
	
	
	
	! Qr=Qin-Mpk[N]/densityw

	!Cfk[0]>= 10
	!Cfk[N]>= 10
	 !Rtq = 1-Qr/Qin
	 
	 ! r1 < 0.99
	 
	 ! r2 > 0.01
	 Cfk[N]>= 10

	 
	 
	 PQHpumpk1 >= 0
	 PQBpumpk1 >= 0
	 PQHpumpj1 >= 0
	 PQBpumpj1 >= 0
	 PQLpumpj1 >= 0
	! SEC< 49
	 ! Pfj[N]= Pmaxj1_
	 ! Pfk[0]= Pmaxk1_
	 ! Cfk[0]>=10
	 
	!maximize Rt
	!minimize SEC
	minimize LCOW
	
   End Equations
 End Model